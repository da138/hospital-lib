apply plugin: 'java-library'
apply plugin: "jacoco"
apply plugin: 'com.novoda.bintray-release' // must be applied after your artifact generating plugin (eg. java / com.android.library)

publish {
    userOrg = 'keryhu'// bintray 账户名。
    groupId = 'com.sdrin.lib'
    artifactId = 'hospital-lib'
    publishVersion = '1.0.5'
    desc = '上海石指健康医院公共库'
    website = 'https://github.com/keryhu/hospital-lib'
}
description = 'hospital-lib'
group = 'com.sdrin.lib'
version = '1.0.5'

buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.9.2'
    }
}

repositories {
    mavenCentral()
    google()
    jcenter()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.apache.httpcomponents:httpclient:4.5.12"
    implementation "org.bouncycastle:bcpkix-jdk15on:1.65"
    implementation "com.google.code.gson:gson:2.8.6"

    testImplementation "org.junit.jupiter:junit-jupiter:5.5.2"
    testImplementation "com.google.code.gson:gson:2.8.6"
}
configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}


// 发布测试覆盖率，目前这个在 腾讯云上表现一般
jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}
task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = "sources"
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
    exclude '**/*.fxml', '**/*.css', '**/*.csv', '**/*.ps1', '**/*.json', '**/*.jpg', '**/*.png'
}

javadoc {
    options {
        memberLevel = JavadocMemberLevel.PRIVATE
        windowTitle = "${rootProject.name} ${version} "
        encoding = 'UTF-8'
        charSet 'UTF-8'
        author = true
        use = true
    }
    doFirst {
        javadoc.title = "上海石指健康医院lib库"
        javadoc.options.docTitle = javadoc.title
    }
}

sourceCompatibility = 11
targetCompatibility = 11
